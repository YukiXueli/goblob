apply plugin: 'protobuf'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'ws.antonov.gradle.plugins:gradle-plugin-protobuf:0.9.1'
    }
}

repositories {
    mavenCentral()
}

def protobufversion() {
    Process result = [ project.convention.plugins.protobuf.protocPath, "--version" ].execute()
    def outputBuffer = new StringBuffer()
    result.consumeProcessOutput(outputBuffer, outputBuffer)
    result.waitFor()
    def output = outputBuffer.toString()
    def match = output =~ /libprotoc ([0-9.]*).*/
    def version = match[0][1]
    println "Using libprotoc: " + version
    return version
}

dependencies {
    compile group: "com.google.protobuf", name: "protobuf-java", version: protobufversion()
}

// AndroidStudio can't see the sources from build/generated-sources without the following
// workaround.
task copyGeneratedToMain(type: Copy) {
    from 'build/generated-sources/main'
    into 'src/main/java'
}
task deleteGeneratedSources(type: Delete) {
    delete 'build/generated-sources/main'
}
task deleteMainSources(type: Delete) {
    delete 'src/main/java'
}
copyGeneratedToMain.dependsOn(generateProto)
deleteGeneratedSources.dependsOn(copyGeneratedToMain)
compileJava.dependsOn(deleteGeneratedSources)
clean.dependsOn(deleteMainSources)
