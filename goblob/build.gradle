buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    compile "com.android.support:appcompat-v7:${appcompat_library_version}"
    compile "com.google.android.gms:play-services-plus:${gms_library_version}"
    compile "com.google.android.gms:play-services-games:${gms_library_version}"
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup.dagger:dagger:1.2.2'
    apt 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile project(':BaseGameUtils')
    compile project(':goblobBase')
}

android {
    compileSdkVersion android_compile_version
    buildToolsVersion android_version

    defaultConfig {
        versionCode 9
        versionName "1.0.1"
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_compile_version
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("release.keystore")
            keyAlias "game of go"
            keyPassword getReleasePassword()
            storePassword getReleasePassword()
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

private String getReleasePassword() {
    System.getenv("GOBLOB_KEY_PASSWORD")
}
