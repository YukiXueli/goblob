buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'jacoco-android'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    compile "com.android.support:appcompat-v7:${appcompat_library_version}"
    compile "com.google.android.gms:play-services-plus:${gms_library_version}"
    compile "com.google.android.gms:play-services-games:${gms_library_version}"
    compile 'com.google.guava:guava:18.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    compile project(':BaseGameUtils')
    compile project(':goblobBase')
}

android {
    compileSdkVersion android_compile_version
    buildToolsVersion android_version

    defaultConfig {
        versionCode 12
        versionName "1.0.4"
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_compile_version
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("release.keystore")
            keyAlias "game of go"
            keyPassword getReleasePassword()
            storePassword getReleasePassword()
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
        }

        release {
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'  // For Butterknife.
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

private String getReleasePassword() {
    System.getProperty("GOBLOB_KEY_PASSWORD")
}
